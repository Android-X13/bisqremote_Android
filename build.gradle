buildscript {
    ext.kotlin_version = '1.6.21'
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.2.1'
        classpath 'com.google.gms:google-services:4.3.10'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:10.3.0"
    }
}

plugins {
    id "org.jlleitschuh.gradle.ktlint" version "10.3.0"
    id "io.gitlab.arturbosch.detekt" version "1.20.0"
    id "io.snyk.gradle.plugin.snykplugin" version "0.4"
}

repositories {
    mavenCentral()
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

allprojects { project ->
    dependencyLocking {
        lockAllConfigurations()
    }
    task resolveAndLockAll {
        doFirst {
            assert gradle.startParameter.writeDependencyLocks
        }
        doLast {
            configurations.findAll {
                // Add any custom filtering on the configurations to be resolved
                it.canBeResolved
            }.each { it.resolve() }
        }
    }
}

detekt {
    buildUponDefaultConfig = true
    allRules = false
}

snyk {
    severity = 'high'
    autoDownload = true
    autoUpdate = true
    arguments = '--all-sub-projects'
}
